/*
 * Copyright (c) Koninklijke Philips N.V., 2015.
 * All rights reserved.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven { url 'http://maartens-mini.ddns.htc.nl.philips.com:8081/artifactory/jcenter' }
    }
    dependencies {

        classpath 'com.android.tools.build:gradle:1.5.0'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'

        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'

        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'

        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.2.0'

        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath('com.terrafolio:gradle-jenkins-plugin:1.2.3') {
            exclude group: 'asm', module: 'asm-tree'
            exclude group: 'asm', module: 'asm'
        }
    }
}

apply from: 'extra/jenkins.gradle'

allprojects {

    apply plugin: 'android-sdk-manager'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.7.1.201405082137'
    }

    repositories {
        maven { url 'http://maartens-mini.ddns.htc.nl.philips.com:8081/artifactory/jcenter' }
    }

    ext._compileSdkVersion = 23
    ext._buildToolsVersion = '23.0.2'
    ext._minSdkVersion = 19
    ext._targetSdkVersion = 23
    ext._versionCode = 3

    ext.shineLibVersion = '0.5.2'

    ext.artifactoryVersion = ext.shineLibVersion
    ext.artifactoryGroup = 'com.philips.cdp.shine'
}

subprojects {
    def coverageSourceDirs = ['src/main/java']

    task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
        group = "Reporting"

        description = "Generate Jacoco coverage reports"

        classDirectories = fileTree(dir: 'build/intermediates/classes/debug',
                excludes: ['**/R.class',
                           '**/R$*.class',
                           '**/*$$ViewBinder*.*',
                           '**/inject/*',
                           '**/*$InjectAdapter.*',
                           '**/BuildConfig.*',
                           '**/Manifest*.*',
                           '**/Dagger*.*',
                           '**/*_Provide*Factory.*',
                           '**/*_Member*Injector.*',
                           '**/*_Factory.*',
                           '**/PagerTitleStripV22*.*'])

        additionalSourceDirs = files(coverageSourceDirs)
        sourceDirectories = files(coverageSourceDirs)
        executionData = files('build/jacoco/testDebugUnitTest.exec')

        doFirst {
            new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
                if (file.name.contains('$$')) {
                    file.renameTo(file.path.replace('$$', '$'))
                }
            }
        }

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

def coverageSourceDirs = ['shinelib/src/main/java']

def coverageExcludes = ['**/R.class',
                        '**/R$*.class',
                        '**/*$$ViewBinder*.*',
                        '**/inject/*',
                        '**/*$InjectAdapter.*',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/Dagger*.*',
                        '**/*_Provide*Factory.*',
                        '**/*_Member*Injector.*',
                        '**/*_Factory.*',
                        '**/PagerTitleStripV22*.*']

def coverageClassDirectories = [fileTree(dir: 'shinelib/build/intermediates/classes/debug', excludes: coverageExcludes)]

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.jacocoTestReport

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    classDirectories = files(coverageClassDirectories)
    executionData = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = false
        csv.enabled = false
    }
    onlyIf = {
        true
    }

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}
