/*
 * Copyright (c) 2015-2017 Koninklijke Philips N.V.
 * All rights reserved.
 */

buildscript { bs ->
    apply from: projectDir.absolutePath + '/../../../../../build-support/gradle/versions.gradle'

    repositories {
        maven {
            url repos.jcenter
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven {
            url repos.plugin
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
    }

    dependencies {
        classpath(builddeps.buildinfoextractor)
        classpath(builddeps.cdpSCM)
    }
}

apply plugin: 'com.android.library'
apply plugin: 'pl.droidsonroids.pitest'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def environment = 'production'
if (project.hasProperty('envCode')) {
    environment = envCode
}

apply plugin: 'cdpSCM'
import com.philips.cdp.cdpSCM

def objcdp = new cdpSCM()
objcdp.setProjectDir(projectDir)
objcdp.setEnv(platform: 'android', env: environment)
archivesBaseName = "commlib-cloud"
version = objcdp.getVersion(versions.cml_commlib)

def zipDoc = file(buildDir.name + "/distributions/$project.name-$version-docs" + '.zip')
artifacts {
    archives zipDoc
}

allprojects {
    repositories {
        maven {
            url "http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-plugins-release-local"
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
    }
}

repositories {
    maven {
        url repos.jcenter
        credentials {
            username = "readonly"
            password = "123qweasdzxc"
        }
    }
    maven {
        url objcdp.getArtifactoryUrl()
        credentials {
            username = "readonly"
            password = "123qweasdzxc"
        }
    }
}

artifactory {
    contextUrl = objcdp.getArtifactoryContextUrl()
    publish {
        repository {
            repoKey = objcdp.getArtifactoryPublishRepoKey()
            username = objcdp.getArtifactoryUploadUser()
            password = objcdp.getArtifactoryUploadPwd()
            defaults {
                publishPom = true
                publishIvy = false
                publishBuildInfo = true
                publishArtifacts = true
                publications('aar')
                properties = ['git.commitid': objcdp.getCommitHashLong()]
            }
        }
    }
    resolve {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
        }
    }
    clientConfig.timeout = 600
    // Artifactory connection timeout (in seconds). The default timeout is 300 seconds.
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId group
            version = version
            artifactId archivesBaseName

            artifact zipDocuments

            artifact("$projectDir/dependencies.lock") {
                classifier = '1stleveldependencies'
            }

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each {
                    def resolvedArtifacts = configurations.compile.getResolvedConfiguration().getResolvedArtifacts()
                    resolvedArtifacts.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.moduleVersion.id.group)
                        dependencyNode.appendNode('artifactId', it.moduleVersion.id.name)
                        dependencyNode.appendNode('version', it.moduleVersion.id.version)
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    defaultConfig {
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        versionCode 1
        versionName version
        buildConfigField "String", "TLA", "\"cml\""
    }

    buildTypes {
        debug {
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}(${objcdp.getCommitHash()})\""
        }
        release {
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}\""
        }
    }

    lintOptions {
        abortOnError false
    }
    publishNonDefault true
}

task generateJavadocPublicApi(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("${project.rootDir}/../Documents/External/${project.name}-api/")

    include {
        if (it.isDirectory) {
            return true
        }
        return it.file.text.contains("* @publicApi")
    }

    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        links("http://docs.oracle.com/javase/7/docs/api/")
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        charSet = 'UTF-8'
    }

    failOnError false
}

dependencies {
    provided deps.support.annotations

    def suffix = objcdp.getVersionSuffix()

    for (dep in [':commlib-api', ':cloudcontroller-api']) {
        if (findProject(dep)) {
            debugCompile project(path: dep, configuration: 'debug')
            releaseCompile project(path: dep, configuration: 'release')
        } else {
            compile("$group$dep:$versions.cml_commlib$suffix") { changing = true }
        }
    }

    for (dep in [':cloudcontroller', ':commlib-testutils']) {
        if (findProject(dep)) {
            testCompile project(path: dep, configuration: 'debug')
        } else {
            testCompile("$group$dep:$versions.cml_commlib$suffix") { changing = true }
        }
    }

    compile deps.gson

    testCompile deps.junit
    testCompile deps.mockito
    testCompile deps.powermock_mockito
    testCompile deps.powermock_junit
    testCompile deps.assertj
}

pitest {
    targetClasses = [
            'com.philips.cdp.dicommclient.port.common.*',
            'com.philips.cdp2.commlib.cloud.*',
            'com.philips.cdp2.commlib.core.*',
            'com.philips.cdp2.commlib.lan.communication.*',
            'com.philips.cdp2.commlib.lan.context.*',
            'com.philips.cdp2.commlib.lan.discovery.*',
            'com.philips.cdp2.commlib.lan.security.*',
            'com.philips.cdp.dicommclient.request.*',
    ]  /* specify target classes to be mutated */
    excludedClasses = [
            'com.philips.cdp2.commlib.core.port.firmware.util.FirmwarePortStateWaiterTest',
            'com.philips.cdp.dicommclient.port.common.PairingHandlerTest',
    ]
    pitestVersion = versions.pitest
    avoidCallsTo = ['com.philips.cdp.dicommclient.util.DICommLog']
    threads = 1 /* specify number of threads */
    outputFormats = ['XML', 'HTML'] /* specify output format */
    reportDir = "${rootDir.absolutePath}/build/report/${project.name}/pitest/"
    timestampedReports = false
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

/**
 * Zips external documentation to be included with a release
 */
task zipDocuments(type: Zip) {
    def docpath = """git rev-parse --show-toplevel""".execute().text.trim()
    def docfiles = docpath + File.separator + "Documents" + File.separator + "External"
    from files(docfiles) {
        baseName = project.name
        classifier 'docs'
    }
}

/**
 * Creates sources.jar for easier debugging
 */
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}