/*
 * This task is for generating jacoco report from connected unit test cases.
 */

apply plugin: 'jacoco'

apply from: project.rootDir.absolutePath + '/build-support/gradle/versions.gradle'

jacoco {
    apply from: buildscript.sourceFile.parent + "/versions.gradle"
    toolVersion versions.jacocoToolVersion
}

/**
 * task to run jacoco connected test report
 */
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination file("${buildDir}/reports/jacoco/jacocoHtml")
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}